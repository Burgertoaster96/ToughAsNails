buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.+', changing: true
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'

repositories { mavenLocal() }

group = "com.github.glitchfiend"
archivesBaseName = "ToughAsNails"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

def getGitHash = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

version = "${minecraft_version}-${mod_version}.${System.getenv().BUILD_NUMBER}"

minecraft {
    mappings channel: 'official', version: minecraft_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client = {
            workingDirectory = project.file("run").canonicalPath
            source sourceSets.main
        }
        server = {
            workingDirectory = project.file("run").canonicalPath
            source sourceSets.main
        }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "10000"
        }
    }
}

// configure  the source folders
sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir "public/src/main/java"
        }
        resources {
            srcDir "src/main/resources"
            srcDir "public/src/main/resources"
        }
    }
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jar {
    classifier = 'universal'
    manifest {
        attributes([
            "Implementation-Version": project.version
        ])
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:' + minecraft_version + '-' + forge_version
    implementation files('libs/SereneSeasons-1.18.1-6.0.0.10-deobf.jar')
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task apiJar(type: Jar) {
    from(sourceSets.main.allJava) {
        include 'toughasnails/api/**'
    }
    from (sourceSets.main.output) {
        include 'LICENSE.txt'
        include 'toughasnails/api/**'
    }

    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    classifier = 'api'
}

def changelog_file = rootProject.file("build/ToughAsNails-${version}-changelog.txt")

curseforge {
    if (project.hasProperty('curseApiKey')) {
        apiKey = project.getProperty('curseApiKey')
        project {
            id = '246391'

            if (changelog_file.exists()) {
                changelog = changelog_file
            }

            releaseType = 'beta'
            addGameVersion minecraft_version

            mainArtifact(jar) {
                displayName = "Tough As Nails ${version}"
            }

            addArtifact deobfJar
            addArtifact apiJar
        }
    }
}

artifacts {
    if (changelog_file.exists()) {
        archives changelog_file
    }
    archives jar
    archives deobfJar
    archives apiJar
}
