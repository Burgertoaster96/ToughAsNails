buildscript {
    repositories {
        mavenLocal()
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'org.spongepowered.mixin'

repositories { mavenLocal() }

group = "com.github.glitchfiend.biomesoplenty"
archivesBaseName = "ToughAsNails"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

def getGitHash = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

version = "${minecraft_version}-${mod_version}.${System.getenv().BUILD_NUMBER}"

minecraft {
    mappings channel: 'snapshot', version: mappings_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client = {
            workingDirectory = project.file("run").canonicalPath
            arg "-mixin.config=toughasnails.mixins.json"
            source sourceSets.main
        }
        server = {
            workingDirectory = project.file("run").canonicalPath
            arg "-mixin.config=toughasnails.mixins.json"
            source sourceSets.main
        }
    }
}

mixin {
    add sourceSets.main, "toughasnails.refmap.json"
}

// configure  the source folders
sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir "public/src/main/java"
        }
        resources {
            srcDir "src/main/resources"
            srcDir "public/src/main/resources"
        }
    }
}


processResources {
    from (sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'version': project.version
    }
}

jar {
    classifier = 'universal'

    manifest {
        attributes([
            "Specification-Title": "toughasnails",
            "Specification-Vendor": "Tough As Nails",
            "Specification-Version": "1.1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"toughasnails",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "toughasnails.mixins.json"
        ])
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:' + minecraft_version + '-' + forge_version
    compile 'org.spongepowered:mixin:0.8'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task apiJar(type: Jar) {
    from(sourceSets.main.allJava) {
        include 'toughasnails/api/**'
    }
    from (sourceSets.main.output) {
        include 'LICENSE.txt'
        include 'toughasnails/api/**'
    }

    classifier = 'api'
}

def changelog_file = rootProject.file("build/ToughAsNails-${version}-changelog.txt")

curseforge {
    if (project.hasProperty('curseApiKey')) {
        apiKey = project.getProperty('curseApiKey')
        project {
            id = '246391'

            if (changelog_file.exists()) {
                changelog = changelog_file
            }

            releaseType = 'alpha'

            mainArtifact(jar) {
                displayName = "Tough As Nails ${version}"
            }

            addArtifact deobfJar
            addArtifact apiJar
        }
    }
}



artifacts {
    if (changelog_file.exists()) {
        archives changelog_file
    }
    archives jar
    archives deobfJar
    archives apiJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty('forgeMavenPassword')) {
                repository(url: "https://files.minecraftforge.net/maven/manage/upload") {
                    authentication(userName: project.getProperty('forgeMavenUsername'), password: project.getProperty('forgeMavenPassword'))
                }
            }
            else {
                // local repo folder. Might wanna juset use  gradle install   if you wanans end it to maven-local
                repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
            }

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'Tough As Nails'
                    url 'https://github.com/Glitchfiend/ToughAsNails'
                
                    scm {
                        url 'https://github.com/Glitchfiend/ToughAsNails'
                        connection 'scm:git:git://github.com/Glitchfiend/ToughAsNails.git'
                        developerConnection 'scm:git:git@github.com:Glitchfiend/ToughAsNails.git'
                    }
                
                    issueManagement {
                        system 'github'
                        url 'https://github.com/Glitchfiend/ToughAsNails/issues'
                    }
                
                    licenses {
                        license {
                            name 'Creative Commons Attribution-NonCommercial-NoDerivs 4.0 International Public License'
                            url 'http://creativecommons.org/licenses/by-nc-nd/4.0/'
                            distribution 'repo'
                        }
                    }
                
                    developers {
                        developer {
                            id 'Adubbz'
                            name 'Adubbz'
                            roles { role 'developer' }
                        }
                        developer {
                            id 'Forstride'
                            name 'Forstride'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}
